# Contributorr: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_cranname=desc
_cranver=1.4.3
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="Manipulate DESCRIPTION Files"
arch=(any)
url="https://cran.r-project.org/package=${_cranname}"
license=(MIT)
depends=('r>=3.4' r-cli r-r6 r-utils)
optdepends=(r-callr r-covr r-gh r-spelling r-testthat r-whoami r-withr)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('54468da73dd78fc9e7c565c41cfe3331802c2134b2e61a9ad197215317092f26')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
