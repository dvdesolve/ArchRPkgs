# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_bcname=VegaMC
_bcver=3.32.0
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="VegaMC: A Package Implementing a Variational Piecewise Smooth Model for Identification of Driver Chromosomal Imbalances in Cancer"
arch=(i686 x86_64)
url="https://bioconductor.org/packages/${_bcname}"
license=(GPL2)
depends=('r>=2.10.0' r-biomart r-biobase)
makedepends=(patch)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_bcname}_${_bcver}.tar.gz"
        "fix_globals.patch")
sha256sums=('d585ab698623f701ea24a8dfe02e128dae6b2685d1ed04fd6b96c9a480ff72a7'
            'a45787150bdf297482a8623da5ab7257adb2828108d797a688abd73e87490233')

prepare() {
  cd "${srcdir}/${_bcname}"

  # fix global variables overlap until it will be fixed in upstream
  patch -Np0 -i "${srcdir}/fix_globals.patch"
}

build() {
  # create staging directory for installation
  mkdir -p "${srcdir}/staged"

  R CMD INSTALL "${_bcname}" -l "${srcdir}/staged"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${srcdir}/staged/${_bcname}" "${pkgdir}/usr/lib/R/library"
}
