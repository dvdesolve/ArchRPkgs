# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: haha662 <haha662 at outlook dot com>

_cranname=blogdown
_cranver=1.7
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="Create Blogs and Websites with R Markdown"
arch=(any)
url="https://cran.r-project.org/package=${_cranname}"
license=(GPL3)
depends=(r 'r-rmarkdown>=2.8' 'r-bookdown>=0.22' 'r-knitr>=1.25' r-htmltools 'r-yaml>=2.1.19' 'r-httpuv>=1.4.0' r-later 'r-xfun>=0.29' r-jsonlite 'r-servr>=0.21' hugo pandoc)
optdepends=(r-testit r-shiny r-miniui r-stringr r-rstudioapi r-processx r-whoami)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('6abe3409cef999437dc921a3119c27fde1f413be661e459e99eaab32f0ea7a6c')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
