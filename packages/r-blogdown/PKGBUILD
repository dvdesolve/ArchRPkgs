# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: haha662 <haha662 at outlook dot com>

_cranname=blogdown
_cranver=1.16
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="Create Blogs and Websites with R Markdown"
arch=(any)
url="https://cran.r-project.org/package=${_cranname}"
license=(GPL3)
depends=('r>=3.5.0' 'r-bookdown>=0.22' r-htmltools 'r-httpuv>=1.4.0' r-jsonlite 'r-knitr>=1.25' r-later 'r-rmarkdown>=2.8' 'r-servr>=0.21' 'r-xfun>=0.34' 'r-yaml>=2.1.19' hugo pandoc)
optdepends=(r-miniui r-processx r-rstudioapi r-shiny r-stringr r-testit r-whoami)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('c45e2eb46c0292acb4e755c62a64a59e6bd7733f3ca67da953cdf849624ca895')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
