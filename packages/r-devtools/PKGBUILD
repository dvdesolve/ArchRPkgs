# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Kibouo <csonka.mihaly@hotmail.com>
# Contributor: Ward Segers <w@rdsegers.be>
# Contributor: Alex Branham <branham@utexas.edu>

_cranname=devtools
_cranver=2.4.5
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=2
pkgdesc="Tools to Make Developing R Packages Easier"
arch=(any)
url="https://cran.r-project.org/package=${_cranname}"
license=(MIT)
depends=('r>=3.0.2' 'r-usethis>=2.1.6' 'r-cli>=3.3.0' 'r-desc>=1.4.1' 'r-ellipsis>=0.3.2' 'r-fs>=1.5.2' 'r-lifecycle>=1.0.1' 'r-memoise>=2.0.1' 'r-miniui>=0.1.1.1' 'r-pkgbuild>=1.3.1' 'r-pkgdown>=2.0.6' 'r-pkgload>=1.3.0' 'r-profvis>=0.3.7' 'r-rcmdcheck>=1.4.0' 'r-remotes>=2.4.2' 'r-rlang>=1.0.4' 'r-roxygen2>=7.2.1' 'r-rversions>=2.1.1' 'r-sessioninfo>=1.2.2' 'r-testthat>=3.1.5' 'r-urlchecker>=1.0.1' 'r-withr>=2.5.0' r-stats r-tools r-utils)
optdepends=(r-biocmanager r-callr r-covr r-curl r-digest r-dt r-foghorn r-gh r-gmailr r-httr r-knitr r-lintr r-mockery r-pingr r-rhub r-rmarkdown r-rstudioapi r-spelling r-mass)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('38160ebd839acdec7ebf0699a085b4ab1ebd5500d3c57a9fa7ae484f1909904b')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
