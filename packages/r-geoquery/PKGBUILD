# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_bcname=GEOquery
_bcver=2.62.2
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="Get data from NCBI Gene Expression Omnibus (GEO)"
arch=(any)
url="https://bioconductor.org/packages/${_bcname}"
license=(GPL2)
depends=(r r-biobase r-httr 'r-readr>=1.3.1' r-xml2 r-dplyr r-data.table r-tidyr r-magrittr r-r.utils r-limma)
optdepends=(r-knitr r-rmarkdown r-biocgenerics r-testthat r-covr r-markdown)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_bcname}_${_bcver}.tar.gz")
sha256sums=('bcd7b1df3ab89ef14664fb4b2358f655ccc04995d287739440af61522ec8ccc2')

build() {
  R CMD INSTALL ${_bcname}_${_bcver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_bcname}" "${pkgdir}/usr/lib/R/library"
}
