# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_bcname=gpart
_bcver=1.13.0
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="Human genome partitioning of dense sequencing data by identifying haplotype blocks"
arch=(i686 x86_64)
url="https://bioconductor.org/packages/${_bcname}"
license=(MIT)
depends=('r>=3.5.0' r-homo.sapiens r-txdb.hsapiens.ucsc.hg38.knowngene r-igraph r-biomart r-rcpp r-data.table r-organismdbi r-annotationdbi r-genomicranges r-iranges)
makedepends=(patch)
optdepends=(r-knitr r-rmarkdown r-biocstyle r-testthat)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_bcname}_${_bcver}.tar.gz"
        "fix_pi.patch")
sha256sums=('4d8b149f072e10ad1ad149bf09325871cb08d740511bcaef9b4c08aea6e42894'
            '033774f380e8c8edbe056b8dd2b076bcd408895a4ecaa75eb6da5e1c99caae70')

prepare() {
  cd "${srcdir}/${_bcname}"

  # fix misspelled PI macro/variable
  patch -Np0 -i "${srcdir}/fix_pi.patch"
}

build() {
  # create staging directory for installation
  mkdir -p "${srcdir}/staged"

  R CMD INSTALL "${_bcname}" -l "${srcdir}/staged"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${srcdir}/staged/${_bcname}" "${pkgdir}/usr/lib/R/library"
}
