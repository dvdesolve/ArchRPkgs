# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_bcname=GO.db
_bcver=3.14.0
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="A set of annotation maps describing the entire Gene Ontology"
arch=(any)
url="https://bioconductor.org/packages/${_bcname}"
license=(Artistic2.0)
depends=('r>=2.7.0' 'r-annotationdbi>=1.55.1')
optdepends=(r-dbi)
source=("https://bioconductor.org/packages/release/data/annotation/src/contrib/${_bcname}_${_bcver}.tar.gz")
sha256sums=('45d0a681a662667d45b2472d160b72f7058ad0a28dd0ca24742e11ddfd87d8e7')

build() {
  R CMD INSTALL ${_bcname}_${_bcver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_bcname}" "${pkgdir}/usr/lib/R/library"
}
