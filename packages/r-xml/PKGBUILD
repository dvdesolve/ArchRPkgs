# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_cranname=XML
_cranver=3.99-0.14
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="Tools for Parsing and Generating XML Within R and S-Plus"
arch=(i686 x86_64)
url="https://cran.r-project.org/package=${_cranname}"
license=(BSD)
depends=('r>=4.0.0' r-methods r-utils libxml2)
optdepends=(r-bitops r-rcurl)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('2cb6a61a4d8d89e311994f47df09913d4ce5281317d42c78af4aafd75a31f1f9')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
