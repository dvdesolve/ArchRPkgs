# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Ward Segers <w@rdsegers.be>
# Contributor: Alex Branham <alex.branham@gmail.com>

_cranname=rstan
_cranver=2.32.6
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="R Interface to Stan"
arch=(i686 x86_64)
url="https://cran.r-project.org/package=${_cranname}"
license=(GPL3)
depends=('r>=3.4.0' 'r-stanheaders>=2.32.0' 'r-ggplot2>=3.3.5' 'r-inline>=0.3.19' 'r-gridextra>=2.3' 'r-rcpp>=1.0.7' 'r-rcppparallel>=5.1.4' 'r-loo>=2.4.1' 'r-pkgbuild>=1.2.0' 'r-rcppeigen>=0.3.4.0.0' 'r-bh>=1.75.0.0' r-methods r-stats4 r-quickjsr pandoc)
makedepends=(make)
optdepends=(r-shinystan r-bayesplot r-rmarkdown r-rstantools r-rstudioapi r-knitr r-v8 r-kernsmooth r-matrix r-parallel r-coda r-testthat)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('3390d00191bbd3b0739dd19fe437b99a041a6b04be208877b48419d1348a1a70')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
