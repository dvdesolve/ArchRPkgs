# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_cranname=pillar
_cranver=1.9.0
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="Coloured Formatting for Columns"
arch=(any)
url="https://cran.r-project.org/package=${_cranname}"
license=(MIT)
depends=(r 'r-cli>=2.3.0' r-fansi r-glue r-lifecycle 'r-rlang>=1.0.2' 'r-utf8>=1.1.0' 'r-vctrs>=0.5.0' r-utils)
optdepends=(r-bit64 r-debugme r-diagrammer r-dplyr r-formattable r-ggplot2 r-knitr r-lubridate r-nanotime r-nycflights13 r-palmerpenguins r-rmarkdown r-scales r-stringi r-testthat r-tibble r-units r-vdiffr r-withr r-dbi r-survival)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('f23eb486c087f864c2b4072d5cba01d5bebf2f554118bcba6886d8dbceb87acc')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
