# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_bcname=networkBMA
_bcver=2.34.0
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="Regression-based network inference using Bayesian Model Averaging"
arch=(i686 x86_64)
url="https://bioconductor.org/packages/${_bcname}"
license=(GPL2)
depends=('r>=2.15.0' r-bma 'r-rcpp>=0.10.3' 'r-rcpparmadillo>=0.3.810.2' 'r-rcppeigen>=0.3.1.2.1' r-leaps r-bh lapack)
makedepends=(patch)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_bcname}_${_bcver}.tar.gz"
        "fix_boostinc.patch")
sha256sums=('0fdd36382a7775a4e6dfbe2b511f0c853471303a0d062a0c62240928dc8a2703'
            'fe3b4f88661542b0053dc01af9755514e1f889166a68dd1735df29d7e459cfbf')

prepare() {
  cd "${srcdir}/${_bcname}"

  # fix issue with missing Boost header inclusion
  patch -Np0 -i "${srcdir}/fix_boostinc.patch"
}

build() {
  # create staging directory for installation
  mkdir -p "${srcdir}/staged"

  R CMD INSTALL "${_bcname}" -l "${srcdir}/staged"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${srcdir}/staged/${_bcname}" "${pkgdir}/usr/lib/R/library"
}
