# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Matt Frichtl <frichtlm@gmail.com>
# Contributor: Alex Branham <branham@utexas.edu>

_cranname=dbplyr
_cranver=2.3.0
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="A 'dplyr' Back End for Databases"
arch=(any)
url="https://cran.r-project.org/package=${_cranname}"
license=(MIT)
depends=('r>=3.1' 'r-assertthat>=0.2.0' 'r-blob>=1.2.0' 'r-cli>=3.4.1' 'r-dbi>=1.0.0' 'r-dplyr>=1.0.10' 'r-glue>=1.2.0' 'r-lifecycle>=1.0.3' r-magrittr 'r-pillar>=1.5.0' 'r-purrr>=1.0.1' 'r-r6>=2.2.2' 'r-rlang>=1.0.6' 'r-tibble>=1.4.2' 'r-tidyselect>=1.2.0' 'r-vctrs>=0.5.0' r-withr)
optdepends=(r-bit64 r-covr r-knitr r-lahman r-nycflights13 r-odbc r-rmariadb r-rmarkdown r-rpostgres r-rpostgresql r-rsqlite r-testthat r-tidyr)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('070d456e7f2926b4b787b79008c7e00452c94ead9dbaf2631d29be4b68151fff')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
