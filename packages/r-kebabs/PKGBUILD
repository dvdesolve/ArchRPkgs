# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_bcname=kebabs
_bcver=1.28.0
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="Kernel-Based Analysis Of Biological Sequences"
arch=(i686 x86_64)
url="https://bioconductor.org/packages/${_bcname}"
license=(GPL2.1)
depends=('r>=3.2.0' 'r-biostrings>=2.35.5' r-kernlab 'r-rcpp>=0.11.2' 'r-xvector>=0.7.3' 'r-s4vectors>=0.27.3' r-e1071 r-liblinear r-apcluster r-iranges)
makedepends=(patch)
optdepends=(r-sparsem r-biobase r-biocgenerics r-knitr)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_bcname}_${_bcver}.tar.gz"
        "fix_strictrhdrs.patch")
sha256sums=('98305512fffa397b38d7d412fb7de59b5320b9290d03308f06df25aa576ea410'
            'ee468bac3f162ee1d3a23ad59fb8ea5a050e03eb9a96d35950e16fbadc1b95ee')

prepare() {
  cd "${srcdir}/${_bcname}"

  # fix issues with transition to the strict R headers
  patch -Np0 -i "${srcdir}/fix_strictrhdrs.patch"
}

build() {
  # create staging directory for installation
  mkdir -p "${srcdir}/staged"

  R CMD INSTALL "${_bcname}" -l "${srcdir}/staged"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${srcdir}/staged/${_bcname}" "${pkgdir}/usr/lib/R/library"
}
