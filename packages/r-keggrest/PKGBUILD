# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_bcname=KEGGREST
_bcver=1.34.0
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="Client-side REST access to the Kyoto Encyclopedia of Genes and Genomes (KEGG)"
arch=(any)
url="https://bioconductor.org/packages/${_bcname}"
license=(Artistic2.0)
depends=('r>=3.5.0' r-httr r-png r-biostrings)
optdepends=(r-runit r-biocgenerics r-knitr r-markdown)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_bcname}_${_bcver}.tar.gz")
sha256sums=('e988ab805547e542f468bbe0a423f05e9416eb7ba60470e05429ce7c6688e3da')

build() {
  R CMD INSTALL ${_bcname}_${_bcver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_bcname}" "${pkgdir}/usr/lib/R/library"
}
