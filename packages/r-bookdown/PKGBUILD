# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: haha662 <haha662 at outlook dot com>

_cranname=bookdown
_cranver=0.24
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="Authoring Books and Technical Documents with R Markdown"
arch=(any)
url="https://cran.r-project.org/package=${_cranname}"
license=(GPL3)
depends=(r 'r-htmltools>=0.3.6' 'r-knitr>=1.31' 'r-rmarkdown>=2.9' r-jquerylib 'r-xfun>=0.22' 'r-tinytex>=0.12' 'r-yaml>=2.1.19' 'pandoc>=1.17.2')
optdepends=(r-bslib r-downlit r-htmlwidgets r-jsonlite r-rstudioapi r-miniui r-rsconnect r-servr r-shiny r-tibble r-testit r-tufte r-xml2 r-webshgot r-testthat r-withr)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('8bead2a20542d05f643fe77a949689a17b0ae9ff23efbb918ddab47597db1be3')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
