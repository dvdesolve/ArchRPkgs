# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_bcname=MACPET
_bcver=1.14.0
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="Model based analysis for paired-end data"
arch=(i686 x86_64)
url="https://bioconductor.org/packages/${_bcname}"
license=(GPL3)
depends=('r>=3.6.1' 'r-interactionset>=1.13.0' 'r-bigmemory>=4.5.33' 'r-bh>=1.66.0.1' 'r-rcpp>=1.0.1' 'r-intervals>=0.15.1' 'r-plyr>=1.8.4' 'r-rsamtools>=2.1.3' 'r-genomicranges>=1.37.14' 'r-s4vectors>=0.23.17' 'r-iranges>=2.19.10' 'r-genomeinfodb>=1.21.1' 'r-gtools>=3.8.1' 'r-genomicalignments>=1.21.4' 'r-knitr>=1.23' 'r-rtracklayer>=1.45.1' 'r-biocparallel>=1.19.0' 'r-rbowtie>=1.25.0' 'r-geoquery>=2.53.0' 'r-biostrings>=2.53.2' 'r-shortread>=1.43.0' 'r-futile.logger>=1.4.3')
makedepends=(patch)
optdepends=(r-ggplot2 r-igraph r-rmarkdown r-reshape2 r-biocstyle)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_bcname}_${_bcver}.tar.gz"
        "fix_dbleps.patch")
sha256sums=('4b72a9178233e22efc49366e28024312ba01165908b59dc1e440033d262efd20'
            '2b22a1d373b9685991d80ea9177e3f085f50c229b995f4ee1be97510fd65632f')

prepare() {
  cd "${srcdir}/${_bcname}"

  # fix issue with some global macro
  patch -Np0 -i "${srcdir}/fix_dbleps.patch"
}

build() {
  # create staging directory for installation
  mkdir -p "${srcdir}/staged"

  R CMD INSTALL "${_bcname}" -l "${srcdir}/staged"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${srcdir}/staged/${_bcname}" "${pkgdir}/usr/lib/R/library"
}
