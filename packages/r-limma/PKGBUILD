# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_bcname=limma
_bcver=3.52.0
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="Linear Models for Microarray Data"
arch=(i686 x86_64)
url="https://bioconductor.org/packages/${_bcname}"
license=(GPL2)
depends=('r>=3.6.0')
optdepends=(r-affy r-annotationdbi r-biasedurn r-biobase r-ellipse r-go.db r-gplots r-illuminaio r-locfit r-org.hs.eg.db r-statmod r-vsn)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_bcname}_${_bcver}.tar.gz")
sha256sums=('34e8c9d8c438c4c1cf85ac4c38ff6f6b5bd6cd1e9a60eec45f19877a9766fdc3')

build() {
  R CMD INSTALL ${_bcname}_${_bcver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_bcname}" "${pkgdir}/usr/lib/R/library"
}
