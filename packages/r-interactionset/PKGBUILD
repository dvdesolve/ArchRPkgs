# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_bcname=InteractionSet
_bcver=1.26.0
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="Base Classes for Storing Genomic Interaction Data"
arch=(i686 x86_64)
url="https://bioconductor.org/packages/${_bcname}"
license=(GPL3)
depends=(r-genomicranges r-summarizedexperiment r-rcpp r-biocgenerics 'r-s4vectors>=0.27.12' r-iranges r-genomeinfodb)
optdepends=(r-testthat r-knitr r-rmarkdown r-biocstyle)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_bcname}_${_bcver}.tar.gz")
sha256sums=('1c584faed3cdf5852b136c935d8e9c6babf85d9d3df9868457443ce1003acb91')

build() {
  R CMD INSTALL ${_bcname}_${_bcver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_bcname}" "${pkgdir}/usr/lib/R/library"
}
