# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Charles Vejnar

_bcname=DESeq2
_bcver=1.38.3
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="Differential gene expression analysis based on the negative binomial distribution"
arch=(i686 x86_64)
url="https://bioconductor.org/packages/${_bcname}"
license=(LGPL3)
depends=(r 'r-s4vectors>=0.23.18' r-iranges r-genomicranges 'r-summarizedexperiment>=1.1.6' 'r-biocgenerics>=0.7.5' r-biobase r-biocparallel r-matrixstats r-locfit r-geneplotter r-ggplot2 'r-rcpp>=0.11.0' r-rcpparmadillo)
optdepends=(r-testthat r-knitr r-rmarkdown r-vsn r-pheatmap r-rcolorbrewer r-apeglm r-ashr r-tximport r-tximeta r-tximportdata r-readr r-pbapply r-airway r-pasilla r-glmgampoi r-biocmanager)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_bcname}_${_bcver}.tar.gz")
sha256sums=('b5db24957eefca903089d9b6ca56db25438a25ad3981ce83fc847eb3647a3e4f')

build() {
  R CMD INSTALL ${_bcname}_${_bcver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_bcname}" "${pkgdir}/usr/lib/R/library"
}
