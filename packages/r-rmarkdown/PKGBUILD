# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: anzi2001 <anzi2001 at gmail dot com>
# Contributor: haha662 <haha662 at outlook dot com>
# Contributor: Kibouo <csonka.mihaly@hotmail.com>
# Contributor: Ward Segers <w@rdsegers.be>
# Contributor: Alex Branham <branham@utexas.edu>

_cranname=rmarkdown
_cranver=2.24
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="Dynamic Documents for R"
arch=(any)
url="https://cran.r-project.org/package=${_cranname}"
license=(GPL3)
depends=('r>=3.0' 'r-bslib>=0.2.5.1' 'r-evaluate>=0.13' 'r-htmltools>=0.5.1' r-jquerylib r-jsonlite 'r-knitr>=1.22' 'r-stringr>=1.2.0' 'r-tinytex>=0.31' 'r-xfun>=0.36' 'r-yaml>=2.1.19' r-methods 'r-fontawesome>=0.5.0' r-utils r-tools pandoc)
optdepends=(r-digest r-dygraphs r-fs r-rsconnect r-downlit r-katex r-sass r-shiny r-testthat r-tibble r-vctrs r-withr r-cleanrmd)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('3873efa46df4b1d2613b49d345d6442cd1a332e08594c204c8fa177edcd53d86')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
