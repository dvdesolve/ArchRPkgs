# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Grey Christoforo <first name at last name dot net>

_cranname=BiocManager
_cranver=1.30.16
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="Access the Bioconductor Project Package Repository"
arch=(any)
url="https://cran.r-project.org/package=${_cranname}"
license=(Artistic-2.0)
depends=(r)
optdepends=(r-biocversion r-remotes r-rmarkdown r-testthat r-withr r-curl r-knitr)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('75a754a55192ef6aa6ac9b054fd5381ff03fe6bb8b2e033eb8143da930ef3855')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
