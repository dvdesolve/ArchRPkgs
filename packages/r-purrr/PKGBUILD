# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: frichtlm <frichtlm@gmail.com>

_cranname=purrr
_cranver=1.0.2
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="Functional Programming Tools"
arch=(i686 x86_64)
url="https://cran.r-project.org/package=${_cranname}"
license=(MIT)
depends=('r>=3.5.0' 'r-cli>=3.6.1' 'r-lifecycle>=1.0.3' 'r-magrittr>=1.5.0' 'r-rlang>=1.1.1' 'r-vctrs>=0.6.3')
optdepends=(r-covr r-dplyr r-httr r-knitr r-lubridate r-rmarkdown r-testthat r-tibble r-tidyselect)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('2c1bc6bb88433dff0892b41136f2f5c23573b335ff35a4775c72aa57b48bbb63')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
