# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_bcname=biomaRt
_bcver=2.54.0
pkgname=r-${_bcname,,}
pkgver=${_bcver//[:-]/.}
pkgrel=1
pkgdesc="Interface to BioMart databases (i.e. Ensembl)"
arch=(any)
url="https://bioconductor.org/packages/${_bcname}"
license=(Artistic2.0)
depends=(r 'r-xml>=3.99.0.7' r-annotationdbi r-progress r-stringr r-httr r-digest r-biocfilecache r-rappdirs r-xml2)
optdepends=(r-biocstyle r-knitr r-mockery r-rmarkdown r-testthat)
source=("https://bioconductor.org/packages/release/bioc/src/contrib/${_bcname}_${_bcver}.tar.gz")
sha256sums=('989a1ac3c9f99833c277892dfc868f0aebc8ac106ce4d038766e4d514c7cca30')

build() {
  R CMD INSTALL ${_bcname}_${_bcver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_bcname}" "${pkgdir}/usr/lib/R/library"
}
