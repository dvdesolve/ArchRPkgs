# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com
# Contributor: Matt Frichtl <frichtlm@gmail.com>
# Contributor: Kibouo <csonka.mihaly@hotmail.com>
# Contributor: Ward Segers <w@rdsegers.be>
# Contributor: Alex Branham <alex.branham@gmail.com>

_cranname=testthat
_cranver=3.2.1
pkgname=r-${_cranname,,}
pkgver=${_cranver//[:-]/.}
pkgrel=1
pkgdesc="Unit Testing for R"
arch=(i686 x86_64)
url="https://cran.r-project.org/package=${_cranname}"
license=(MIT)
depends=('r>=3.6.0' 'r-brio>=1.1.3' 'r-callr>=3.7.3' 'r-cli>=3.6.1' 'r-desc>=1.4.2' 'r-digest>=0.6.33' 'r-evaluate>=0.21' 'r-jsonlite>=1.8.7' 'r-lifecycle>=1.0.3' 'r-magrittr>=2.0.3' 'r-pkgload>=1.3.2.1' 'r-praise>=1.0.0' 'r-processx>=3.8.2' 'r-ps>=1.7.5' 'r-r6>=2.5.1' 'r-rlang>=1.1.1' 'r-waldo>=0.5.1' 'r-withr>=2.5.0' r-methods r-utils)
optdepends=(r-covr r-curl r-diffviewer r-knitr r-rmarkdown r-rstudioapi r-shiny r-usethis r-vctrs r-xml2)
source=("https://cran.r-project.org/src/contrib/${_cranname}_${_cranver}.tar.gz")
sha256sums=('1d980e611b01c194007639a100c3ddaeaab77786d32f81680f497f99e60748ad')

build() {
  R CMD INSTALL ${_cranname}_${_cranver}.tar.gz -l "${srcdir}"
}

package() {
  install -dm0755 "${pkgdir}/usr/lib/R/library"

  cp -a --no-preserve=ownership "${_cranname}" "${pkgdir}/usr/lib/R/library"
}
